структура РидерДанных
    пер ПутьКФайлу:Строка = "input.txt"
    метод ПрочитатьДанные(): Массив<Строка>
        знч ФайлДанных = новый Файл(ПутьКФайлу)
        исп Поток = ФайлДанных.ОткрытьПотокЧтения()
        возврат Поток.ПрочитатьКакСтроку().ПолучитьСтроки()
    ;
;

структура Билет
    пер Выигрыш: Массив<Строка>
    пер Номера: Массив<Строка>

    метод ЗаполнитьНомера(ВыигрышныеНомера: Строка, МоиНомера:Строка)

        Выигрыш = ВыигрышныеНомера.Разделить(" ")
        Номера = МоиНомера.Разделить(" ")

    ;

    метод ВыигрышныхКомбинаций():Число
        знч Пересечение = (новый Множество(Выигрыш).Пересечение(новый Множество(Номера)))
        возврат Пересечение.Размер()
    ;

;

метод Скрипт()

    знч Данные = новый РидерДанных().ПрочитатьДанные()
    знч Билеты = новый Соответствие<Строка, Билет>()

    знч ШаблонРазбор = новый Образец("^Card ([\\ц\\п]+): ([\\ц\\п]+)\\|([\\ц\\п]+)")

    для СтрокаДанных из Данные

        знч Врем = ШаблонРазбор.НайтиСовпадения(СтрокаДанных)[0]
        знч НомерБилета = Врем.Группа(1)
        знч НовыйБилет = новый Билет()
        НовыйБилет.ЗаполнитьНомера(Врем.Группа(2), Врем.Группа(3))

        Билеты.Вставить(НомерБилета, НовыйБилет)

    ;

    // Первая часть

    пер Результат = 0
    для Билет из Билеты

        знч Выиграл = Билет.Значение.ВыигрышныхКомбинаций()

        Результат+=Выиграл == 0 ? 0 : 2**(Выиграл-1)
        
    ; 

    Консоль.Записать(Результат)
    // Вторая часть

    пер КоэфБилетов = новый Соответствие<Число, Число>()

    для Билет из Билеты

        знч НомерБилета = новый Число(Билет.Ключ.Сократить())
        КоэфБилетов[НомерБилета] = КоэфБилетов.ПолучитьИлиУмолчание(НомерБилета, 0) + 1 //оригинал

        для сч = 1 по Билет.Значение.ВыигрышныхКомбинаций()

            КоэфБилетов[НомерБилета + сч] = КоэфБилетов.ПолучитьИлиУмолчание(НомерБилета + сч, 0) + КоэфБилетов[НомерБилета]

        ;

    ;

    Результат = 0
    для Врем из КоэфБилетов

        Результат+=Врем.Значение

    ;

    Консоль.Записать(Результат)

;