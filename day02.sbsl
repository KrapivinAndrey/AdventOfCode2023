структура РидерДанных
    обз пер ПутьКФайлу:Строка
    метод ПрочитатьДанные(): Массив<Строка>
        знч ФайлДанных = новый Файл(ПутьКФайлу)
        исп Поток = ФайлДанных.ОткрытьПотокЧтения()
        возврат Поток.ПрочитатьКакСтроку().ПолучитьСтроки()
    ;
;

метод Скрипт()

    знч Данные = новый РидерДанных("input.txt")

    //знч КонтрольнаяСумма = ПосчитатьКонтрольнуюСумму(Данные.ПрочитатьДанные())
    //Консоль.Записать(КонтрольнаяСумма)

    знч Мощность = ПосчитатьМощностьИгры(Данные.ПрочитатьДанные())
    Консоль.Записать(Мощность)
    
;

метод ПосчитатьКонтрольнуюСумму(ВхДанные:Массив<Строка>): Число
    пер Результат = 0

    знч Образец = '^Game (\d+): (.*)'

    для ХодИгры из ВхДанные

        пер Разбор = Образец.НайтиСовпадения(ХодИгры)
        знч Ид = новый Число(Разбор[0].Группа(1))
        знч Ходы = Разбор[0].Группа(2)

        если ВалидныйХод1(Ходы)
            Результат+=Ид
        ;

        

    ;

    возврат Результат
;

метод ПосчитатьМощностьИгры(ВхДанные:Массив<Строка>): Число
    пер Результат = 0

    знч Образец = '^Game (\d+): (.*)'

    для ХодИгры из ВхДанные

        пер Разбор = Образец.НайтиСовпадения(ХодИгры)
        Результат+=МощностьИгры(Разбор[0].Группа(2))

    ;

    возврат Результат
;

метод ВалидныйХод1(СтрокаХода:Строка):Булево

    знч Шаблон = новый Образец("[,;]")
    пер Ходы = СтрокаХода.Разделить(Шаблон)

    для Запись из Ходы
        знч КолКуб = Запись.Разделить(" ")
        знч Кубиков = новый Число(КолКуб[0])
        знч ЦветКубика = КолКуб[1]

        если ЦветКубика == "red" и Кубиков > 12
                или ЦветКубика == "green" и Кубиков > 13
                или ЦветКубика == "blue" и Кубиков > 14

            возврат Ложь
        ;
    ;

    возврат Истина

;

метод МощностьИгры(СтрокаИгры: Строка):Число

    пер Красных = 0
    пер Зеленых = 0
    пер Синих = 0

    для ЧастьХода из СтрокаИгры.Разделить(";")
        для Запись из ЧастьХода.Разделить(", ")
            знч КолКуб = Запись.Разделить(" ")
            знч Кубиков = новый Число(КолКуб[0])
            знч ЦветКубика = КолКуб[1]

            если ЦветКубика == "red" и Красных < Кубиков
                Красных = Кубиков
            ;
            если ЦветКубика == "green" и Зеленых < Кубиков
                Зеленых = Кубиков
            ;
            если ЦветКубика == "blue" и Синих < Кубиков
                Синих = Кубиков
            ;
        ;
    ;

    возврат Красных*Зеленых*Синих

;