структура РидерДанных
    пер ПутьКФайлу:Строка = "input.txt"
    метод ПрочитатьДанные(): Массив<Строка>
        знч ФайлДанных = новый Файл(ПутьКФайлу)
        исп Поток = ФайлДанных.ОткрытьПотокЧтения()
        возврат Поток.ПрочитатьКакСтроку().ПолучитьСтроки()
    ;
;

метод СтрокаВЧисло(ВхСтрока: Строка):Число
    возврат новый Число(ВхСтрока.Сократить())
;

метод РешитьКвадратноеУравнение(а: Число, б:Число, ц:Число):Массив<Число>

    знч Дискриминант = б*б - 4 * а * ц
    если Дискриминант < 0
        выбросить новый ИсключениеНедопустимыйАргумент("Дискриминант меньше 0")
    ;

    знч Первое = (-б + Корень(Дискриминант)) / (2*а)
    знч Второе = (-б - Корень(Дискриминант)) / (2*а)

    возврат [(Первое+1).ЦелаяЧасть(), (Второе-1).Округлить(РежимОкругления.Вверх)]
;

метод Скрипт()
    знч Данные = новый РидерДанных().ПрочитатьДанные()

    знч Время = Данные[0].Заменить("Time: ", "").Разделить(" ")
    знч Путь = Данные[1].Заменить("Distance: ", "").Разделить(" ")
    пер Результат = 1
    для сч=0 по Время.Размер()-1
        знч Границы = РешитьКвадратноеУравнение(-1, СтрокаВЧисло(Время[сч]), -СтрокаВЧисло(Путь[сч]))
        Консоль.Записать(Границы)
        Результат=Результат*(Границы[1] - Границы[0] + 1)
    ;

    Консоль.Записать(Результат)

    // вторая часть 

    знч ТруВремя = СтрокаВЧисло(Данные[0].Заменить("Time: ", "").Заменить(" ", ""))
    знч ТруПуть = СтрокаВЧисло(Данные[1].Заменить("Distance: ", "").Заменить(" ", ""))

    знч Границы = РешитьКвадратноеУравнение(-1, ТруВремя, -ТруПуть)

    Консоль.Записать(Границы[1] - Границы[0] + 1)

;